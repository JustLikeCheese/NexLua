plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.vanniktech.maven.publish)
    id 'com.kezong.fat-aar'
}

android {
    namespace 'com.nexlua'
    compileSdk {
        version = release(36)
    }

    defaultConfig {
        minSdk 21
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    externalNativeBuild {
        cmake {
            version '3.22.1'
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
    ndkVersion '27.0.12077973'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    embed project(path: ':luajava:core', configuration: 'default')
    implementation libs.nextoast
}

mavenPublishing {
    coordinates("io.github.justlikecheese.nexlua", "NexLua", "1.1.0")
    publishToMavenCentral()
    signAllPublications()

    pom {
        name = "NexLua"
        description = "A high-performance LuaJIT framework for Android"
        inceptionYear = "2025"
        url = "https://github.com/JustLikeCheese/NexLua/"
        licenses {
            license {
                name = "MIT License"
                url = "https://opensource.org/licenses/MIT"
                distribution = "repo"
            }
        }
        developers {
            developer {
                id = "JustLikeCheese"
                name = "JustLikeCheese"
                url = "https://github.com/JustLikeCheese/"
            }
        }
        scm {
            url = "https://github.com/JustLikeCheese/NexLua"
            connection = "scm:git://github.com/JustLikeCheese/NexLua.git"
            developerConnection = "scm:git:ssh://git@github.com/JustLikeCheese/NexLua.git"
        }
    }
}