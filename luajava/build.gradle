plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.vanniktech.maven.publish)
    id 'com.kezong.fat-aar'
}

android {
    namespace 'com.luajava'
    compileSdkVersion 36

    defaultConfig {
        minSdk 21
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    embed project(path: ':luajava:core', configuration: 'default')
    embed project(path: ':luajava:native', configuration: 'default')
}

mavenPublishing {
    coordinates("io.github.justlikecheese.nexlua", "LuaJava", "1.1.2")
    publishToMavenCentral()
    signAllPublications()

    pom {
        name = "LuaJava"
        description = "A high-performance LuaJIT framework for Android"
        inceptionYear = "2025"
        url = "https://github.com/JustLikeCheese/NexLua/"
        licenses {
            license {
                name = "MIT License"
                url = "https://opensource.org/licenses/MIT"
                distribution = "repo"
            }
        }
        developers {
            developer {
                id = "JustLikeCheese"
                name = "JustLikeCheese"
                url = "https://github.com/JustLikeCheese/"
            }
        }
        scm {
            url = "https://github.com/JustLikeCheese/NexLua"
            connection = "scm:git://github.com/JustLikeCheese/NexLua.git"
            developerConnection = "scm:git:ssh://git@github.com/JustLikeCheese/NexLua.git"
        }

        withXml {
            asNode().dependencies.'*'.findAll {
                it.groupId.text().startsWith('NexLua.')
            }.each {
                it.parent().remove(it)
            }
        }
    }
}